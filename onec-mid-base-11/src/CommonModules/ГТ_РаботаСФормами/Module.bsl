#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт  
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПолеСогласованнаСкидкаИКнопкавГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоПередПолем(Форма,Форма.Элементы.СуммаДокумента);  
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоПередПолем(Форма,Форма.Элементы.Основание);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеКонтактноеЛицо = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеКонтактноеЛицо.ПутьКДанным = "Объект.ГТ_КонтактноеЛицо";
	
КонецПроцедуры 

Процедура ДобавитьПолеКонтактноеЛицоПередПолем(Форма, ПолеВставки)

	ПолеКонтактноеЛицо = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,ПолеВставки);
	ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеКонтактноеЛицо.ПутьКДанным = "Объект.ГТ_КонтактноеЛицо";	 
	
КонецПроцедуры

Процедура ДобавитьПолеСогласованнаСкидкаИКнопкавГруппаШапкаЛево(Форма)
	
	ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
 	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеСогласованнаяСкидка = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), ГруппаСкидка);
	ПолеСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСогласованнаяСкидка.ПутьКДанным = "Объект.ГТ_СогласованнаяСкидка";
	ПолеСогласованнаяСкидка.УстановитьДействие("ПриИзменении","СогласованнаяСкидкаПриИзменении");
	
	КомандаПересчет = Форма.Команды.Добавить("ПересчитатьСоСкидкой");
	КомандаПересчет.Заголовок = "Пересчитать суммы";
	КомандаПересчет.Действие = "ПересчитатьСоСкидкойНажатие";
	
	КнопкаПересчет = Форма.Элементы.Добавить("КнопкаПересчет", Тип("КнопкаФормы"), ГруппаСкидка);
	КнопкаПересчет.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаПересчет.ИмяКоманды = "ПересчитатьСоСкидкой";
	
КонецПроцедуры

#КонецОбласти
